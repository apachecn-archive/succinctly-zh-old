# 第 3 章功能编程

你在[第 1 章](../Text/fs-2.html#_Chapter_1_)中看到，纯函数式编程将所有东西视为一个值，包括函数。虽然 F# 不是一种纯函数式语言，但它确实鼓励你以函数式编程;也就是说，它鼓励您使用返回结果的表达式和计算，而不是导致某些副作用的语句。在本章中，我们将调查支持函数式编程范例的 F# 的主要语言结构，并了解它们如何使其更容易在函数式编程中进行编程。

## 文字

Literals 表示常量值，是计算的有用构建块。 F# 有一组丰富的文字，我们将在下一个例子中看到。

在 F# 中，字符串文字可以包含换行符，常规字符串文字可以包含标准转义码。逐字字符串文字使用反斜杠（\）作为常规字符，两个双引号（`""`）是引号的转义码。您可以使用适当的前缀和后缀指示符，使用十六进制和八进制定义所有整数类型。以下示例显示了一些与标识符绑定的动作中的文字，这些标题在本章后面的[标识符和让绑定](#_Identifiers_and_let)中描述。

```
// Some strings.
let message = "Hello
World\r\n\t!"
let dir = @"c:\projects"

// A byte array.
let bytes = "bytesbytesbytes"B

// Some numeric types.
let xA = 0xFFy
let xB = 0o7777un
let xC = 0b10010UL

```

## 功能

在 F# 中，使用关键字`fun`定义函数。函数的参数由空格分隔，参数通过 ASCII 箭头（`-&gt;`）与函数体分隔。

下面是一个函数示例，它接受两个值并将它们相加：

```
fun x y -> x + y

```

请注意，此函数没有名称;这是一种函数文字。以这种方式定义的函数被称为匿名函数，lambda 函数，或者只是 lambdas。

函数不需要名称的想法可能看起来有点奇怪。但是，如果要将函数作为参数传递给另一个函数，则可能不需要名称，尤其是当它执行的任务相对简单时。

如果需要为函数指定名称，可以将其绑定到标识符，如下一节所述。

## 标识符并让绑定

标识符是您为 F# 中的值命名的方式，因此您可以稍后在程序中引用它们。您可以使用关键字`let`，后跟标识符的名称，等号以及指定标识符所引用的值的表达式来定义标识符。表达式是表示将返回值的计算的任何代码。以下表达式显示分配给标识符的值：

```
let x = 42

```

对于来自命令式编程背景的大多数人来说，这看起来像一个变量赋值。有许多相似之处，但关键的区别在于，在纯函数式编程中，一旦将值分配给标识符，它就不会改变。这就是为什么我会在本书中将它们称为标识符，而不是变量。

| ![](img/note.png) | 注意：在某些情况下，您可以重新定义标识符。这可能看起来有点像标识符更改值，但它略有不同。此外，在 F# 中的命令式编程中，标识符的值在某些情况下可能会发生变化。在本章中，我们将重点放在函数式编程中，其中标识符不会更改其值。 |

标识符可以指代值或函数，并且因为 F# 函数本身就是值，所以这并不令人惊讶。这意味着 F# 没有函数名称或参数名称的真实概念;这些只是标识符。您可以将匿名函数绑定到标识符，就像将字符串或整数文字绑定到标识符一样：

```
let myAdd = fun x y -> x + y

```

但是，由于需要使用名称定义函数是很常见的，因此 F# 为此提供了一种简短的语法。您以与值标识符相同的方式编写函数定义，但函数在`let`关键字和等号之间具有两个或更多标识符，如下所示：

```
let raisePowerTwo x = x ** 2.0

```

第一个标识符是函数名称`raisePowerTwo`，其后面的标识符是函数参数`x`的名称。如果函数具有名称，强烈建议您使用此较短的语法来定义它。

在 F# 中声明值和函数的语法是无法区分的，因为函数是值，而 F# 语法对它们进行类似处理。例如，请考虑以下代码：

```
let n = 10
let add a b = a + b

let result = add n 4
printfn "%i" (result)

```

在第一行，将值`10`分配给标识符`n`。在第二行，定义了一个`add`函数，它接受两个参数并将它们加在一起。请注意语法有多相似，唯一的区别是函数具有在函数名称后面列出的参数。由于所有内容都是 F# 中的值，因此第一行的文字`10`是一个值，下一行中的表达式`a + b`的结果也是一个自动成为`add`函数结果的值。请注意，不需要像在命令式语言中那样显式地返回函数中的值。

## 标识符名称

管理标识符名称有一些规则。标识符必须以下划线（`_`）或字母开头，然后可以包含任何字母数字字符，下划线或单引号（`'`）。关键字不能用作标识符。由于 F# 支持使用单引号作为标识符名称的一部分，因此您可以使用它来表示“prime”以为不同但相似的值创建标识符名称，如下例所示：

```
let x = 42
let x' = 43

```

F# 支持 Unicode，因此您可以使用非拉丁字母表中的重音字符和字母作为标识符名称：

```
let 标识符 = 42

```

如果管理标识符名称的规则过于严格，则可以使用双刻度标记（````）来引用标识符名称。这允许您使用任何字符序列 - 只要它不包括制表符，换行符或双刻度 - 作为标识符名称。这意味着您可以创建一个以问号结尾的标识符（例如，某些程序员认为在表示布尔值的名称末尾添加问号很有用）：

```
let ``more? `` = true

```

如果您需要使用关键字作为标识符或类型名称，这也很有用：

```
let ``class`` = "style"

```

例如，您可能需要使用未使用 F# 编写的库中的成员，并使用 F# 的关键字作为其名称。通常，最好避免过度使用此功能，因为它可能导致难以使用其他.NET 语言的库。

## 范围

标识符的范围定义了程序中可以使用标识符（或类型，如下一章[定义类型](../Text/fs-5.html#_Defining_Types)部分中所讨论的）的位置。了解范围非常重要，因为如果尝试使用不在范围内的标识符，则会收到编译错误。

所有标识符 - 无论它们与函数还是值有关 - 都是从它们的定义末尾开始，直到它们出现的部分结束。因此，对于位于顶层的标识符（即，不是另一个函数或其他值的本地标识符），标识符的范围是从它定义的位置到源文件的末尾。一旦为顶层的标识符分配了值（或函数），就不能更改或重新定义该值。标识符仅在其定义结束后才可用，这意味着通常不可能根据自身定义标识符。

您会注意到在 F# 中，您永远不需要显式返回值;计算结果自动绑定到其关联的标识符。那么，如何计算函数中的中间值？在 F# 中，这是由空格控制的。缩进创建一个新范围，该范围的结尾在缩进结束时发出信号。缩进意味着`let`绑定是计算中的一个中间值，在该范围之外是不可见的。当一个范围关闭（通过缩进结束）并且标识符不再可用时，可以说超出范围或者超出范围。

为了演示范围，以下示例显示了一个计算两个整数之间的点的函数。第三和第四行显示计算的中间值。

```
// Function to calculate a midpoint.
let halfWay a b =
    let dif = b - a
    let mid = dif / 2
    mid + a

printfn "%i" (halfWay 10 20)

```

首先，计算两个数字之间的差异，并使用`let`关键字将其分配给标识符`dif`。为了表明这是函数中的中间值，它缩进了四个空格。空格数的选择留给程序员，但约定是四。之后，该示例计算中点，使用相同的缩进将其分配给标识符`mid`。最后，函数的期望结果是中点加`a`，因此代码可以简单地说`mid + a`，这就成了函数的结果。

| ![](img/note.png) | 注意：您不能使用制表符而不是空格来缩进，因为在不同的文本编辑器中这些可能看起来不同，这会在空白很重要时导致问题。 |

## 捕获标识符

您已经在 F# 中看到，您可以在其他函数中定义函数。这些函数可以使用范围中的任何标识符，包括定义它们的函数的本地定义。因为这些内部函数是值，所以它们可以作为函数的结果返回，或者作为参数传递给另一个函数。这意味着虽然在函数内定义了一个标识符，使得它对其他函数不可见，但是它的实际生命周期可能比定义它的函数长得多。让我们看一个例子来说明这一点。考虑以下函数，定义为`calculatePrefixFunction`：

```
// Function that returns a function to
let calculatePrefixFunction prefix =
    // calculate prefix.
    let prefix' = Printf.sprintf "[%s]: " prefix
    // Define function to perform prefixing.
    let prefixFunction appendee =
        Printf.sprintf "%s%s" prefix' appendee
    // Return function.
    prefixFunction

// Create the prefix function.
let prefixer = calculatePrefixFunction "DEBUG"

// Use the prefix function.
printfn "%s" (prefixer "My message")

```

该函数返回它定义的内部函数`prefixFunction`。标识符`prefix'`被定义为函数`calculatePrefixFunction`范围的本地标识符; `calculatePrefixFunction`以外的其他功能无法看到它。内部函数`prefixFunction`使用`prefix'`，因此当返回`prefixFunction`时，值`prefix'`仍然可用。 `calculatePrefixFunction`创建功能`prefixer`。调用`prefixer`时，您会看到其结果使用的计算值与`prefix'`相关联：

[DEBUG]：我的留言

虽然您应该了解这个过程，但大多数时候您不需要考虑它，因为它不涉及程序员的任何额外工作。编译器将自动生成闭包，以处理将本地值的生命周期延长到定义它的函数之外。 .NET 垃圾收集将自动处理从内存中清除值。当以命令式样式编程时，理解在闭包中捕获的标识符的这一过程可能更为重要，其中标识符可以表示随时间变化的值。在函数式编程时，标识符将始终表示不变的值，从而使得更容易弄清楚闭包中捕获的内容。

## 递归

递归意味着根据自身定义函数;换句话说，该函数在其定义中调用自身。递归通常用于函数式编程，在命令式编程中使用循环。许多人认为，当用递归而不是循环表示时，算法更容易理解。

要在 F# 中使用递归，请在`let`关键字后使用`rec`关键字使标识符在函数定义中可用。以下示例显示了递归操作。请注意，在第五行，函数如何将自身作为自己定义的一部分进行两次调用。

```
// A function to generate the Fibonacci numbers.
let rec fib x =
    match x with
    | 1 -> 1
    | 2 -> 1
    | x -> fib (x - 1) + fib (x - 2)

// Call the function and print the results.
printfn "(fib 2) = %i" (fib 2)
printfn "(fib 6) = %i" (fib 6)
printfn "(fib 11) = %i" (fib 11)   

```

该函数计算 Fibonacci 序列中的 n 项。 Fibonacci 序列是通过在序列中添加前两个数字生成的，并且进展如下：1,1,2,3,5,8,13 .......递归最适合计算 Fibonacci 序列，因为除了前两个序列之外，序列中任何数字的定义取决于能够计算前两个数字，因此 Fibonacci 序列是根据其自身定义的。

尽管递归是一种强大的工具，但在使用它时应该小心。很容易无意中编写一个永不终止的递归函数。尽管故意编写一个不终止的程序有时很有用，但在尝试执行计算时很少是目标。为了确保递归函数终止，根据基本情况和递归情况考虑递归通常很有用：

*   递归情况是根据自身定义函数的值。对于函数`fib`，这是除 1 和 2 之外的任何值。
*   基本案例是非递归案例;也就是说，必须有一些值，其中函数没有根据自身定义。在`fib`功能中，1 和 2 是基本情况。

拥有基本案例本身并不足以确保终止。递归情况必须倾向于基本情况。在`fib`示例中，如果`x`大于或等于 3，则递归情况将倾向于基本情况，因为`x`将始终变小并且在某些点达到 2.但是，如果`x`如果小于 1，则`x`将继续增加更多负值，并且该函数将重复，直到达到机器的极限，从而导致堆栈溢出错误（`System.StackOverflowException`）。

前面的代码也使用 F# 模式匹配，本章后面的[模式匹配](#_Pattern_Matching)部分对此进行了讨论。

## 运算符

在 F# 中，您可以将运算符视为一种更美观的调用函数的方法。

F# 有两种不同的运算符：

*  前缀运算符是操作符位于运算符之后的运算符。
*  中缀运算符位于第一个和第二个操作数之间。

F# 提供了丰富多样的运算符集，您可以将它们与数字，布尔值，字符串和集合类型一起使用。 F# 及其库中定义的运算符太多，无法在本节中介绍，因此我们将研究如何在 F# 中使用和定义运算符，而不是查看单个运算符。

与在 C# 中一样，F# 运算符被重载，这意味着您可以将多个类型与运算符一起使用。但是，与 C# 不同，两个操作数必须是相同的类型，否则编译器将生成错误。 F# 还允许用户定义和重新定义运算符。

运算符遵循一系列类似于 C# 的规则来实现运算符重载分辨率;因此，BCL 中的任何类或任何为支持 C# 中的运算符重载而编写的.NET 库都将在 F# 中支持它。例如，您可以使用`+`运算符来连接字符串，以及将`System.TimeSpan`添加到`System.DateTime`，因为这些类型支持`+`运算符的重载。以下示例说明了这一点：

```
let rhyme = "Jack " + "and " + "Jill"
printfn "%string" rhyme

open System
let oneYearLater =
    DateTime.Now + new TimeSpan(365, 0, 0, 0, 0)
printfn "%A" oneYearLater

```

与函数不同，运算符不是值，因此它们不能作为参数传递给其他函数。但是，如果需要使用运算符作为值，则可以通过括号括起来执行此操作。然后，操作员将表现得像一个功能。实际上，这有两个后果：

*   运算符现在是一个函数，其参数将出现在运算符之后：

```
let result = (+) 1 1

```

*   由于它是一个值，它可以作为函数的结果返回，传递给另一个函数或绑定到标识符。这提供了一种非常简洁的方法来定义`add`功能：

```
let add = (+)

```

当我们查看使用列表时，您将看到如何在本章后面使用运算符作为值。

## 功能应用

函数应用程序，有时也被称为函数组成或组成函数，简单地意味着调用带有一些参数的函数。以下示例显示正在定义的`add`函数，然后应用于两个参数。请注意，参数不用括号或逗号分隔;只需要空格来分隔它们。

```
let add x y = x + y

let result = add 4 5

printfn "(add 4 5) = %i" result

```

编译和执行时，此示例的结果如下：

（加 4 5）= 9

在 F# 中，函数具有固定数量的参数，并应用于源文件中下一个显示的值。在调用函数时，您不一定需要使用括号，但 F# 程序员经常使用它们来定义应该将哪个函数应用于哪些参数。考虑使用`add`功能添加四个数字的简单情况。您可以将每个函数调用的结果绑定到一个新标识符，但是对于这样一个简单的计算，这将非常麻烦：

```
let add x y = x + y

let result1 = add 4 5
let result2 = add 6 7

let finalResult = add result1 result2

```

相反，通常最好将一个函数的结果直接传递给下一个函数。为此，请使用括号显示哪些参数与哪些功能相关联：

```
let add x y = x + y

let result =
    add (add 4 5) (add 6 7)

```

这里，`add`函数的第二次和第三次出现分别与参数`4`，`5`和`6`，`7`分组，并且`add`函数的第一次出现将作用于其他两个功能的结果。

F# 还提供了另一种使用管道转发运算符（`|&gt;`）组合函数的方法。此运算符具有以下定义：

```
let (|>) x f = f x

```

这只是意味着它接受一个参数`x`，并将其应用于给定的函数`f`，以便现在在函数之前给出参数。以下示例显示了使用管道转发运算符应用于函数`System.Math.Cos`的参数`0.5`：

```
let result = 0.5 |> System.Math.Cos

```

在某些情况下，这种反转可能很有用，尤其是当您想要将许多功能链接在一起时。以下是使用管道转发运算符重写的先前`add`函数示例：

```
let add x y = x + y

let result = add 6 7 |> add 4 |> add 5

```

一些程序员认为这种风格更具可读性，因为它具有以更加从右到左的方式读取代码的效果。现在应该将代码读作“添加 6 到 7，将此结果转发到下一个将添加 4 的函数，然后将此结果转发到将添加 5 的函数”。

此示例还利用了在 F# 中部分应用函数的功能，这将在下一节中讨论。

## 功能的部分应用

F# 支持部分应用功能（这些功能有时称为部分或 curried 功能）。这意味着您不需要立即将所有参数传递给函数。请注意，上一节中的最后一个示例将单个参数传递给`add`函数，该函数接受两个参数。这与函数是值的想法非常相关。所以我们可以创建一个`add`函数，将一个参数传递给它，并将结果函数绑定到一个新的标识符：

```
let add x y = x + y

let addFour = add 4

```

因为函数只是一个值，如果它没有立即接收到它的所有参数，它返回一个值，它是一个等待其余参数的新函数。所以在这个例子中，只将值`4`传递给`add`函数会产生一个新函数。我命名函数`addFour`，因为它接受一个参数并将值`4`添加到它。乍一看，这个想法可能看起来无趣且无益，但它是函数式编程的一个强大功能，您将在本书中看到它。

## 模式匹配

模式匹配允许您查看标识符的值，然后根据其值进行不同的计算。它可能与 C ++和 C# 中的`switch`语句进行比较，但它更强大，更灵活。以函数式编写的程序往往被写为应用于输入数据的一系列转换。模式匹配允许您分析输入数据并决定应该对其应用哪种变换，因此模式匹配与功能样式中的编程非常吻合。

F# 中的模式匹配构造允许您对各种类型和值进行模式匹配。它在语言的几个地方也有几种不同的形式和作物。

最简单的模式匹配形式是匹配值。您已经在本章的 [Recursion](#_Recursion) 部分中看到了这一点，它用于实现在 Fibonacci 序列中生成数字的函数。为了说明语法，下一个例子显示了一个函数的实现，它将产生 Lucas 数，一系列数字如下：1,3,4,7,11,18,29,47,76 ....... Lucas 序列与 Fibonacci 序列具有相同的定义;只有起点不同。

```
// Definition of Lucas numbers using pattern matching.
let rec luc x =
    match x with
    | x when x <= 0 -> failwith "value must be greater than 0"
    | 1 -> 1
    | 2 -> 3
    | x -> luc (x - 1) + luc (x - 2)

```

模式匹配的语法使用关键字`match`，后跟要匹配的标识符，然后是关键字`with`，然后是所有可能的匹配规则，由管道（`|`）分隔。在最简单的情况下，规则由常量或标识符组成，后跟箭头（`-&gt;`），然后是值与规则匹配时使用的表达式。在函数`luc`的定义中，第二和第三种情况是文字 - 值`1`和`2` - 这些将分别用值`1`和`3`替换。第四种情况将匹配`x`大于 2 的任何值，这将导致另外两次调用`luc`功能。

规则按照定义的顺序进行匹配，如果模式匹配不完整，编译器将发出错误;也就是说，如果有一些可能的输入值与任何规则都不匹配。如果省略了最终规则，则`luc`函数就是这种情况，因为`x`的任何大于 2 的值都不符合任何规则。如果有任何规则永远不会匹配，编译器也会发出警告，通常是因为前面有另一个更常规的规则。如果第四条规则在第一条规则之前移动，则`luc`功能就是这种情况。在这种情况下，其他规则都不会匹配，因为第一个规则将匹配`x`的任何值。

您可以添加`when`防护（如示例中的第一个规则），以准确控制规则何时触发。 `when`保护由关键字`when`后跟布尔表达式组成。匹配规则后，将评估`when`子句，并且仅当表达式求值为`true`时才会触发规则。如果表达式的计算结果为`false`，则将搜索剩余的规则以寻找另一个匹配项。第一条规则被设计为函数的错误处理程序。规则的第一部分是将匹配任何整数的标识符，但`when`保护意味着规则将仅匹配小于或等于零的整数。

如果需要，可以省略第一个`|`。当模式匹配很小并且您希望将其放在一行上时，这可能很有用。您可以在下一个示例中看到这一点，该示例还演示了使用下划线（`_`）作为通配符。

```
let booleanToString x =
    match x with false -> "False" | _ -> "True"

```

`_`将匹配任何值，并且是告诉编译器您对使用此值不感兴趣的一种方式。例如，在此`booleanToString`函数中，您不需要在第二个规则中使用常量`true`，因为如果第一个规则匹配，则您知道`x`的值将为`true`。此外，您不需要使用`x`派生字符串`"True"`，因此您可以忽略该值并仅使用`_`作为通配符。

模式匹配的另一个有用功能是，您可以通过使用管道（`|`）将两种模式组合成一个规则。下一个例子`stringToBoolean`证明了这一点。

```
// Function for converting a Boolean to a string.
let booleanToString x =
    match x with false -> "False" | _ -> "True"

// Function for converting a string to a Boolean.
let stringToBoolean x =
    match x with
    | "True" | "true" -> true
    | "False" | "false" -> false
    | _ -> failwith "unexpected input"

```

前两个规则有两个字符串应该评估为相同的值，因此您可以在两个模式之间使用`|`，而不是使用两个单独的规则。

也可以在 F# 定义的大多数类型上进行模式匹配。接下来的两个示例演示了元组上的模式匹配，两个函数实现了布尔和和或使用模式匹配。每种方法都略有不同。

```
let myOr b1 b2 =
    match b1, b2 with
    | true, _ -> true
    | _, true -> true
    | _ -> false

let myAnd p =
    match p with
    | true, true -> true
    | _ -> false

```

`myOr`函数有两个布尔参数，放在`match`和`with`关键字之间，用逗号分隔以形成元组。 `myAnd`函数有一个参数，它本身就是一个元组。无论哪种方式，为元组创建模式匹配的语法都与创建元组的语法相同。

如果需要匹配元组中的值，则常量或标识符用逗号分隔，标识符或常量的位置定义它在元组中匹配的内容。这在`myOr`功能的第一和第二规则以及`myAnd`功能的第一个规则中显示。这些规则将元组的部分与常量匹配，但如果您希望稍后在规则定义中使用元组的各个部分，则可以使用标识符。仅仅因为你正在使用元组并不意味着你总是需要查看构成元组的各个部分。

`myOr`的第三个规则和`myAnd`的第二个规则显示整个元组与单个`_`通配符匹配。如果要使用规则定义后半部分中的值，也可以使用标识符替换它。

因为模式匹配是 F# 中的常见任务，所以该语言提供了替代的简写语法。如果函数的唯一目的是模式匹配某些东西，那么使用这种语法可能是值得的。在此版本的模式匹配语法中，使用关键字`function`，将模式放置在函数的参数通常所在的位置，然后将所有替代规则与`|`分开。下一个示例在一个简单的函数中显示了这个语法，该函数递归地处理字符串列表并将它们连接成一个字符串。

```
// Concatenate a list of strings into a single string.
let rec conactStringList =
    function head :: tail -> head + conactStringList tail
           | [] -> ""

// Test data.
let jabber = ["'Twas "; "brillig, "; "and "; "the "; "slithy "; "toves "; "..."]
// Call the function.
let completJabber = conactStringList jabber
// Print the result.
printfn "%s" completJabber

```

编译和执行时，此示例的结果如下：

'Twas brillig，以及狡猾的 toves ......

模式匹配是 F# 的基本构建块之一，我们将在本书中多次回到它。我们将在下一章中查看具有记录类型和联合类型的列表上的模式匹配。

## 控制流程

F# 具有控制流的强烈概念。这样，它与许多纯函数语言不同，其中控制流的概念非常宽松，因为表达式可以基本上以任何顺序进行评估。控制流的强烈概念在`if… then… else…`表达中很明显。

在 F# 中，`if… then… else…`构造是一个表达式，意味着它返回一个值。将返回两个不同值中的一个，具体取决于`if`和`then`关键字之间的布尔表达式的值。下一个例子说明了这一点。根据程序是在偶数秒还是奇数秒运行，评估`if… then… else…`表达式返回`"heads"`或`"tails"`。

```
let result =
    if System.DateTime.Now.Second % 2 = 0 then
        "heads"
    else
        "tails"

printfn "%A" result        

```

有趣的是，`if… then… else…`表达式只是布尔值上模式匹配的便捷缩写。上一个示例可以重写如下：

```
let result =
    match System.DateTime.Now.Second % 2 = 0 with
    | true -> "heads"
    | false ->  "tails"

printfn "%A" result

```

如果您更熟悉命令式编程，那么`if… then… else…`表达式会有一些您可能不会想到的含义。 F# 的类型系统要求`if… then… else…`表达式返回的值必须是相同的类型，否则编译器将生成错误。因此，如果在前面的示例中，您使用整数或布尔值替换了字符串`"tails"`，则会出现编译错误。如果您确实要求值为不同类型，则可以创建`obj`类型的`if… then… else…`表达式（F# 的`System.Object`版本），如下一个示例所示，其中打印`"heads"`或`false`到控制台。

```
let result =
    if System.DateTime.Now.Second % 2 = 0 then
        box "heads"
    else
        box false

printfn "%A" result

```

如果表达式返回值，则`if` `… then… else…`表达式必须具有`else`，因此命令程序员可能会感到惊讶。当您考虑刚刚看到的示例时，这是合乎逻辑的。如果从代码中删除`else`，则当`if`评估为假时，无法为标识符`result`分配值，并且具有未初始化的标识符是 F# （和一般的函数编程）旨在避免的。

## 列表

F# 列表是 F# 内置的简单集合类型。 F# 列表可以是空列表，由方括号（`[]`）表示，或者它可以是具有连接到它的值的另一个列表。您使用内置运算符将值连接到 F# 列表的前面，该运算符由两个冒号（`::`）组成，发音为“cons”。下一个示例显示了一些正在定义的列表，从第一行的空列表开始，后跟两个列表，其中字符串通过串联放置在前面：

```
let emptyList = []
let oneItem = "one " :: []
let twoItem = "one " :: "two " :: []

```

通过连接将项添加到列表的语法有点冗长，因此如果您只想定义列表，可以使用速记。在这种简写表示法中，将列表项放在方括号之间，并用分号（`;`）分隔，如下所示：

```
let shortHand = ["apples "; "pears"]

```

在列表上工作的另一个 F# 运算符是“at”符号（`@`），您可以使用它将两个列表连接在一起，如下所示：

```
let twoLists = ["one, "; "two, "] @ ["buckle "; "my "; "shoe "]

```

F# 列表中的所有项目必须属于同一类型。如果您尝试将不同类型的项放在列表中 - 例如，您尝试将字符串连接到整数列表 - 您将收到编译错误。如果需要混合类型列表，可以创建`obj`类型的列表（F# 等效于`System.Object`），如下面的代码示例所示：

```
// The empty list.
let emptyList = []

// List of one item.
let oneItem = "one " :: []

// List of two items.
let twoItem = "one " :: "two " :: []

// List of two items.
let shortHand = ["apples "; "pairs "]

// Concatenation of two lists.
let twoLists = ["one, "; "two, "] @ ["buckle "; "my "; "shoe "]

// List of objects.
let objList = [box 1; box 2.0; box "three"]

```

我将在下一章[类型和类型推断](../Text/fs-5.html#_Chapter_4_)中更详细地讨论 F# 中的类型。

F# 列表是不可变。换句话说，一旦创建了列表，就无法更改它。作用于列表的函数和运算符不会改变它们，但是它们会创建列表的新修改版本，如果需要，可以将旧列表留待以后使用。下一个例子说明了这一点。

```
// Create a list of one item.
let one = ["one "]
// Create a list of two items.
let two = "two " :: one
// Create a list of three items.
let three = "three " :: two

// Reverse the list of three items.
let rightWayRound = List.rev three

printfn "%A" one
printfn "%A" two
printfn "%A" three
printfn "%A" rightWayRound

```

创建包含单个字符串的 F# 列表，然后创建另外两个列表，每个列表使用前一个列表作为基础。最后，`List.rev`功能应用于最后一个列表以创建新的反转列表。

## 模式匹配列表

使用 F# 列表的常规方法是使用模式匹配和递归。用于从列表中提取头项的模式匹配语法与用于将项连接到列表的语法相同。模式由表示头部的标识符，后跟`::`，然后是列表其余部分的标识符形成。您可以在下一个示例的`concatList`的第一个规则中看到这一点。您还可以对列表常量进行模式匹配;你可以在`concatList`的第二条规则中看到这一点，其中有一个空列表。

```
// List to be concatenated.
let listOfList = [[2; 3; 5]; [7; 11; 13]; [17; 19; 23; 29]]

// Definition of a concatenation function.
let rec concatList l =
    match l with
    | head :: tail -> head @ (concatList tail)
    | [] -> []

// Call the function.
let primes = concatList listOfList

// Print the results.
printfn "%A" primes

```

从列表中取头，处理它，然后递归处理列表的尾部是通过模式匹配处理列表的最常见方式，但它肯定不是你可以用模式匹配和列表做的唯一事情。以下示例显示了此功能组合的一些其他用法。

```
// Function that attempts to find various sequences.
let rec findSequence l =
    match l with
    // Match a list containing exactly 3 numbers.
    | [x; y; z] ->
        printfn "Last 3 numbers in the list were %i %i %i"
            x y z
    // Match a list of 1, 2, 3 in a row.
    | 1 :: 2 :: 3 :: tail ->
        printfn "Found sequence 1, 2, 3 within the list"
        findSequence tail
    // If neither case matches and items remain,
    // recursively call the function.
    | head :: tail -> findSequence tail
    // If no items remain, terminate.
    | [] -> ()

// Some test data.
let testSequence = [1; 2; 3; 4; 5; 6; 7; 8; 9; 8; 7; 6; 5; 4; 3; 2; 1]

// Call the function.
findSequence testSequence

```

第一条规则演示了如何匹配固定长度的列表 - 在这种情况下，是三个项目的列表。这里，标识符用于获取这些项的值，以便可以将它们打印到控制台。第二条规则查看列表中的前三项，看它们是否是整数序列 1,2,3;如果是，它会向控制台输出一条消息。最后两个规则是列表的标准头尾处理，旨在通过列表工作，如果与前两个规则不匹配则不执行任何操作。

编译和执行时，此示例的结果如下：

在列表中找到序列 1,2,3

列表中的最后 3 个数字是 3 2 1

虽然模式匹配是分析列表中数据的强大工具，但通常不需要直接使用它。 F# 库提供了许多高阶函数，用于处理为您实现模式匹配的列表，因此您无需重复代码。为了说明这一点，假设你需要编写一个函数，为列表中的每个项添加一个函数。您可以使用模式匹配轻松编写：

```
let rec addOneAll list =
    match list with
    | head :: rest ->
        head + 1 :: addOneAll rest
    | [] -> []

printfn "(addOneAll [1; 2; 3]) = %A" (addOneAll [1; 2; 3])

```

编译和执行时，此示例的结果如下：

（addOneAll [1; 2; 3]）= [2; 3; 4]

但是，对于这样一个简单的问题，代码可能比您想要的更冗长。解决这个问题的线索来自于注意到在列表中的每个项目中添加一个只是一个更普遍的问题的示例：需要对列表中的每个项目应用一些转换。 F# 核心库包含`List`模块中定义的`map`功能。它具有以下定义：

```
let rec map func list =
    match list with
    | head :: rest ->
        func head :: map func rest
    | [] -> []

```

您可以看到`map`功能与前一个示例中的`addOneAll`功能结构非常相似。如果列表不为空，则获取列表的头项并应用函数`func`，您将作为参数给出。然后将其附加到在列表的其余部分递归调用`map`的结果。如果列表为空，则只返回空列表。然后可以使用`map`函数以更简洁的方式实现向列表中的所有项添加一个：

```
let result = List.map ((+) 1) [1; 2; 3]
printfn "List.map ((+) 1) [1; 2; 3] = %A" result

```

另请注意，此示例使用`add`运算符作为函数，用括号括起它，如本章前面[运算符](#_Operators)部分所述。然后通过传递第一个参数而不是第二个参数来部分应用此函数。这将创建一个取整数并返回一个整数的函数，该整数传递给`map`函数。

`List`模块包含许多其他用于处理列表的有趣函数，例如`List.filter`，它允许您使用谓词过滤列表，`List.fold`，用于创建列表的摘要。

## 摘要

本章简要介绍了使用 F# 的功能特性。这些为程序员提供了一种强大而灵活的方法来创建程序。