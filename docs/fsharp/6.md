# 第 5 章面向对象的编程

面向对象编程是第三个主要的编程范例。试图将功能范式和面向对象的范式展现为竞争的倾向，但我认为它们是可以很好地协同工作的互补技术，我将在本章中尝试展示。从本质上讲，面向对象编程有一些简单的想法，有时也被称为面向对象编程的原则：封装，多态和继承。

可能最重要的原则是 _ 封装 _ - 实现和状态应该是 _ 封装 _，或隐藏在明确定义的边界之后的想法。这使得程序的结构更易于管理。在 F# 中，您可以通过使用模块和类型定义的签名来隐藏事物，也可以通过简单地将它们定义为表达式或类构造（您将在本章中看到两者的示例）。

第二个原则是 _ 多态 _，您可以通过多种方式实现抽象实体。您已经遇到了许多简单的抽象实体，例如函数类型。函数类型是抽象的，因为您可以通过许多不同的方式实现具有特定类型的函数。例如，您可以将函数类型`int -&gt; int`实现为递增给定参数的函数，递减参数的函数或数百万数学序列中的任何一个。您还可以使用现有的抽象组件构建其他抽象实体，例如.NET BCL 中定义的接口类型。您还可以使用用户定义的接口类型对更复杂的抽象实体进行建模。接口类型的优点是您可以按层次排列它们;这称为 _ 接口继承 _。例如，.NET BCL 包含`System.Collections`和`System.Collections.Generic`命名空间中可用的集合类型的分层分类。

在 OOP 中，您有时可以分层次地安排实现片段。这称为 _ 实现继承 _，由于函数式编程为定义和共享实现片段提供了灵活性，因此它在 F# 编程中往往不那么重要。但是，它对于图形用户界面（GUI）编程等领域非常重要。

虽然面向对象编程的原则很重要，但面向对象编程也成为围绕系统 _ 名词 _ 组织代码的同义词，然后提供对这些值的操作作为成员，函数或方法对此值起作用。这通常就像将函数应用于函数的函数（例如`String.length s`）并使用点符号（例如`s.Length`）重写一样简单。这种简单的行为通常可以使您的代码更清晰。您将在本章中看到 F# 如何允许您将成员附加到其任何类型，而不仅仅是其类，从而使您能够根据需要以面向对象的方式组织所有代码。

F# 提供了丰富的面向对象编程模型，允许您创建类似于 C# 和 VB.NET 创建的类，接口和对象。也许更重要的是，您在 F# 中创建的类与打包在库中并由该库的用户查看时在其他语言中创建的类无法区分。但是，面向对象编程不仅仅是简单地定义对象，正如您将开始研究如何使用 F# native 类型以面向对象的方式编程时所看到的那样。

## F# 与会员类型

可以向 F# 的记录和联合类型添加函数。您可以使用点表示法调用添加到记录或联合类型的函数，就像您可以从未使用 F# 编写的库中的类成员一样。当您希望将在 F# 中定义的类型公开给其他.NET 语言时，它也证明是有用的。许多程序员更喜欢看到对实例值进行的函数调用，这种技术为所有 F# 类型提供了一种很好的方法。

使用成员定义 F# 记录或联合类型的语法与您在[第 4 章](../Text/fs-5.html#_Chapter_4_)中学习的语法相同，除了它包括始终位于`with`关键字之后的成员定义。成员本身的定义以关键字`member`开头，后跟一个标识符，该标识符表示成员所附加类型的参数，后跟一个点，函数名称，然后是函数所采用的任何其他参数。在此之后出现一个等号后跟函数定义，它可以是任何 F# 表达式。

以下示例定义记录类型`Point`。它有两个字段，`Left`和`Top`;和成员函数，`Swap`。 `Swap`功能是一个简单的功能，可以创建一个新的点，交换`Left`和`Top`的值。注意如何在函数定义中使用函数名`Swap`之前给出的`x`参数来访问记录的其他成员：

```
// A point type.
type Point =
    { Top: int;
      Left: int }
    with
        // The swap member creates a new point
        // with the left/top coords reversed.
        member x.Swap() =
            { Top = x.Left;
              Left = x.Top }

// Create a new point.
let myPoint =
    { Top = 3;
      Left = 7 }

let main() =
    // Print the inital point.
    printfn "%A" myPoint
    // Create a new point with the coordinates swapped.
    let nextPoint = myPoint.Swap()
    // Print the new point.
    printfn "%A" nextPoint

// Start the app.
do main()

```

编译并执行此示例时，您将获得以下结果：

{Top = 3;

左= 7;}

{Top = 7;

左= 3;}

您可能已经注意到函数`Swap`定义中的`x`参数：

```
member x.Swap() =
    { Top = x.Left;
      Left = x.Top }

```

这是表示调用函数的对象的参数。现在看一下你在一个值上调用函数的情况：

```
let nextPoint = myPoint.Swap()

```

您调用函数的值将作为参数传递给函数。当您认为函数需要能够访问您调用它的值的字段和方法时，这是合乎逻辑的。某些 OO 语言为此使用特定关键字，例如`this`或`Me`，但是 F# 允许您通过在关键字成员 - `x`之后为其指定名称来选择此参数的名称。

联合类型也可以具有成员函数。您可以使用与定义记录类型相同的方式定义它们。下一个示例显示了一个联合类型`DrinkAmount`，它添加了一个函数：

```
// A type representing the amount of a specific drink.
type DrinkAmount =
    | Coffee of int
    | Tea of int
    | Water of int
    with
        // Get a string representation of the value.
        override x.ToString() =
            match x with
            | Coffee x -> Printf.sprintf "Coffee: %i" x
            | Tea x -> Printf.sprintf "Tea: %i" x
            | Water x -> Printf.sprintf "Water: %i" x

// Create a new instance of DrinkAmount.
let t = Tea 2

// Print out the string.
printfn "%s" (t.ToString())

```

编译并执行此代码时，您将获得以下结果：

茶：2

请注意它如何使用关键字`override`代替关键字`member`。这具有替换或 _ 覆盖 _ 基本类型的现有功能的效果。对于与 F# 类型相关联的函数成员，这不是一种非常常见的做法，因为只有四种方法可以被覆盖：`ToString`，`Equals`，`GetHashCode`和`Finalize`。每种.NET 类型都从`System.Object`继承这些类型。由于其中一些方法与 CLR 交互的方式，我建议覆盖的唯一方法是`ToString`。只有四种方法可用于覆盖，因为记录和联合类型不能作为基类或派生类，因此您不能继承要覆盖的方法（`System.Object`除外）。

## 定义类

您已经看到了很多使用.NET BCL 库中定义的类的示例;接下来，您将学习如何定义自己的类。在面向对象的编程中，类应该为您正在创建的程序或库中使用的一些概念建模。例如，`String`类为一组字符建模，`Process`类为操作系统进程建模。

类是一个类型，因此类定义以`type`关键字开头，后跟类的名称和括号中类的构造函数的参数。接下来是等号，后面是类的成员定义。类的最基本成员称为 _ 方法 _，它是一个可以访问类参数的函数。

下一个示例显示了一个表示用户的类。用户类的构造函数有两个参数：用户名和用户密码的哈希值。您的类提供了两个成员方法：`Authenticate`，用于检查用户的密码是否有效;和`LogonMessage`，它获取用户特定的登录消息：

```
open System.Web.Security
// Give shorter name to password hashing method.
let hash = FormsAuthentication.HashPasswordForStoringInConfigFile

// A class that represents a user.
// Its constructor takes two parameters: the user's
// name and a hash of his or her password.
type User(name, passwordHash) =
    // Hashes the user's password and checks it against
    // the known hash.
    member x.Authenticate(password) =
        let hashResult = hash (password, "sha1")
        passwordHash = hashResult

    // Gets the user's logon message.
    member x.LogonMessage() =
        Printf.sprintf "Hello, %s" name

// Create a new instance of our user class.
let user = User("Robert", "AF73C586F66FDC99ABF1EADB2B71C5E46C80C24A")

let main() =
    // Authenticate user and print appropriate message.
    if user.Authenticate("badpass") then
        printfn "%s" (user.LogonMessage())
    else
        printfn "Logon failed"

do main()

```

该示例的后半部分演示了如何使用该类。它的行为与您从.NET BCL 中看到的其他类完全相同。您可以使用`new`关键字创建`User`的新实例，然后调用其成员方法。

定义类内部的值通常很有用。也许您需要预先计算在多个成员方法之间共享的值，或者您可能需要从外部数据源检索对象的某些数据。要启用此功能，对象可以具有`let`绑定，这些绑定在对象内部，但在对象的所有成员之间共享。将`let`绑定放在类定义的开头，等号后面，但在第一个成员定义之前。这些`let`绑定形成一个隐式构造，在构造对象时执行;如果`let`绑定有任何副作用，这些也会在构造对象时发生。如果需要调用具有`unit`类型的函数，例如在记录对象的构造时，必须在函数调用前加上`do`关键字。

下一个示例通过获取`User`类并稍微修改它来演示私有`let`绑定。现在，类构造函数采用`firstName`和`lastName`，您在`let`绑定中使用它来计算用户的`fullName`。要查看调用具有副作用的函数时会发生什么，可以将用户的`fullName`打印到控制台：

```
// A class that represents a user.
// Its constructor takes three parameters: the user's
// first name, last name, and a hash of his or her password.
type User(firstName, lastName, passwordHash) =
    // Calculate the user's full name and store for later use
    let fullName = Printf.sprintf "%s %s" firstName lastName
    // Print the user's full name as object is being constructed.
    do printfn "User: %s" fullName

    // Hashes the user's password and checks it against
    // the known hash.
    member x.Authenticate(password) =
        let hashResult = hash (password, "sha1")
        passwordHash = hashResult

    // Retrieves the user's full name.
    member x.GetFullname() = fullName

```

注意成员如何访问类'`let`绑定，以及成员`GetFullName`如何返回预先计算的`fullName`值。

通常需要能够在类中更改值。例如，您可能需要提供`ChangePassword`方法来重置`User`类中的用户密码。 F# 为您提供了两种方法来实现这一目标。您可以使对象不可变 - 在这种情况下，您复制对象的参数，随时更改适当的值。通常认为此方法更适合功能样式编程，但如果对象具有大量参数或创建成本昂贵，则可能有点不方便。例如，这样做可能计算成本很高，或者可能需要大量 I / O 来构建它。以下示例说明了此方法。注意在`ChangePassword`方法中如何调用`password`参数上的`hash`函数，将其传递给`User`对象的构造函数以及用户名：

```
// A class that represents a user.
// Its constructor takes two parameters: the user's
// name and a hash of his or her password.
type User(name, passwordHash) =
    // Hashes the user's password and checks it against
    // the known hash.
    member x.Authenticate(password) =
        let hashResult = hash (password, "sha1")
        passwordHash = hashResult

    // Gets the user's logon message.
    member x.LogonMessage() =
        Printf.sprintf "Hello, %s" name

    // Creates a copy of the user with the password changed.
    member x.ChangePassword(password) =
        new User(name, hash password)

```

不可变对象的替代方法是使您想要更改的值变为可变。您可以通过将其绑定到可变`let`绑定来完成此操作。您可以在下一个示例中看到这一点，您将类的参数`passwordHash`绑定到同名的可变`let`绑定：

```
// A class that represents a user.
// Its constructor takes two parameters: the user's
// name and a hash of his or her password.
type User(name, passwordHash) =
    // Store the password hash in a mutable let
    // binding so it can be changed later.
    let mutable passwordHash = passwordHash

    // Hashes the user's password and checks it against
    // the known hash.
    member x.Authenticate(password) =
        let hashResult = hash (password, "sha1")
        passwordHash = hashResult

    // Gets the user's logon message.
    member x.LogonMessage() =
        Printf.sprintf "Hello, %s" name

    // Changes the user's password.
    member x.ChangePassword(password) =
        passwordHash <- hash password

```

这意味着您可以像`ChangePassword`方法一样将`passwordHash`更新为`let`绑定。

## 定义接口

接口只能包含抽象方法和属性，或者您使用关键字`abstract`声明的成员。接口为实现它们的所有类定义 _ 契约 _，公开客户端可以使用的组件，同时使客户端与实际实现隔离。一个类只能从一个基类继承，但它可以实现任意数量的接口。因为实现接口的任何类都可以被视为接口类型，所以接口提供了与多类继承类似的好处，同时避免了该方法的复杂性。

您可以通过定义没有构造函数的类型以及所有成员都是抽象的类来定义接口。以下示例定义了一个声明两个方法的接口：`Authenticate`和`LogonMessage`。注意接口名称如何以大写`I`开头。这是一个严格遵循.NET BCL 的命名约定，您也应该在代码中遵循它。在阅读代码时，它将帮助其他程序员区分类和接口：

```
// An interface "IUser".
type IUser =
    // Hashes the user's password and checks it against
    // the known hash.
    abstract Authenticate: evidence: string -> bool
    // Gets the user's logon message.
    abstract LogonMessage: unit -> string

let logon (user: IUser) =
    // Authenticate user and print appropriate message.
    if user.Authenticate("badpass") then
        printfn "%s" (user.LogonMessage())
    else
        printfn "Logon failed"

```

该示例的后半部分说明了接口的优点。您可以定义使用该接口的函数，而无需了解实现细节。您定义`logon`函数，该函数接受`IUser`参数并使用它来执行登录。然后，此函数将与`IUser`的任何实现一起使用。这在许多情况下非常有用;例如，它使您能够编写一组客户端代码，您可以使用该代码的几种不同实现重用这些代码。

## 实现接口

要实现接口，请使用关键字`interface`，后跟接口名称，关键字`with`，然后使用代码实现接口成员。使用关键字`member`为成员定义添加前缀，但它们与任何方法或属性的定义相同。您可以通过类或结构实现接口;您可以在以下部分中学习如何创建类的详细信息。

下一个示例定义，实现和使用接口。该接口与您在上一节中实现的`IUser`接口相同;在这里你在一个名为`User`的类中实现它：

```
open System.Web.Security
// Give shorter name to password hashing method.
let hash = FormsAuthentication.HashPasswordForStoringInConfigFile
// An interface "IUser".
type IUser =
    // Hashes the user's password and checks it against
    // the known hash.
    abstract Authenticate: evidence: string -> bool
    // Gets the user's logon message.
    abstract LogonMessage: unit -> string

// A class that represents a user.
// Its constructor takes two parameters: the user's
// name and a hash of his or her password
type User(name, passwordHash) =
    interface IUser with
        // Authenticate implementation.
        member x.Authenticate(password) =
            let hashResult = hash (password, "sha1")
            passwordHash = hashResult

        // LogonMessage implementation.
        member x.LogonMessage() =
            Printf.sprintf "Hello, %s" name

// Create a new instance of the user.
let user = User("Robert", "AF73C586F66FDC99ABF1EADB2B71C5E46C80C24A")
// Cast to the IUser interface.
let iuser = user :> IUser
// Get the logon message.
let logonMessage = iuser.LogonMessage()

let logon (iuser: IUser) =
    // Authenticate the user and print the appropriate message.
    if iuser.Authenticate("badpass") then
        printfn "%s" logonMessage
    else
        printfn "Logon failed"   

do logon user

```

请注意，在示例中间您是第一次看到 _ 施放 _;您可以在 [Casting](#_Casting) 部分的章节末尾找到更详细的铸造说明。但是现在这里是对发生的事情的快速总结：标识符`user`通过向下转换运算符`:&gt;`转换为接口`I` `User`：

```
// Create a new instance of the user.
let user = User("Robert", "AF73C586F66FDC99ABF1EADB2B71C5E46C80C24A")
// Cast to the IUser interface.
let iuser = user :> IUser

```

这是必要的，因为接口是在 F# 中明确实现的。在使用`GetLogonMessage`方法之前，必须具有`IUser`类型的标识符，而不仅仅是实现`IUser`的类。在示例结束时，您将以不同的方式解决此问题。函数`logon`采用`IUser`类型的参数：

```
let logon (iuser: IUser) =

```

当您使用实现`IUser`的类调用`logon`时，该类将隐式向下转换为此类型。

## 施法

转换是一种通过抛弃信息来显式改变值的静态类型的方法，这被称为 _upcasting_ ;或者重新发现它，这被称为 _ 向下转换 _。在 F# 中，upcasts 和 downcasts 都有自己的运算符。类型层次结构以顶部的`obj`（或`System.Object`）开头，其下面是所有后代。向上移动将类型向上移动到层次结构中，而向下移动向下移动层次结构中的类型。

Upcast 将值的静态类型更改为其祖先类型之一。这是一个安全的操作。编译器总是可以判断 upcast 是否有效，因为它总是知道一个类型的所有祖先，所以它能够使用静态分析来确定 upcast 是否会成功。向上用冒号表示，然后是大于号（`:&gt;`）。以下代码显示如何使用 upcast 将`string`转换为`obj`：

```
let myObject = ("This is a string" :> obj)

```

通常，在定义包含不同类型的集合时，必须使用 upcast。如果不使用 upcast，编译器将推断该集合具有第一个元素的类型，并在其他类型的元素放置在集合中时给出编译错误。下一个示例演示如何创建控件数组 - 使用 Windows 窗体时的常见任务。请注意，您将所有单个控件向上转换为其公共基类`Control`：

```
open System.Windows.Forms

let myControls =
    [| (new Button() :> Control);
       (new TextBox() :> Control);
       (new Label() :> Control) |]

```

upcast 还具有自动装箱任何值类型的效果。值类型保存在程序堆栈的内存中，而不是托管堆上。 Boxing 意味着将值推送到托管堆，因此可以通过引用传递它。以下示例演示如何设置值：

```
let boxedInt = (1 :> obj)

```

向下转换将值的静态类型更改为其后代类型之一;因此，它恢复了被上流隐藏的信息。向下转换是危险的，因为编译器没有任何方法可以静态地确定类型的实例是否与其派生类型之一兼容。这意味着您可能会出错，这将导致在运行时发出无效的强制转换异常（`System.InvalidCastException`）。由于向下转换的固有危险，许多开发人员更喜欢用.NET 类型的模式匹配来替换它。然而，一个向下转换在一些地方可能是有用的，所以一个向下的运算符 - 由冒号，问号和大于号（`:?&gt;`）组成 - 是可用的。下一个示例显示了如何使用向下转换：

```
open System.Windows.Forms

let moreControls =
    [| (new Button() :> Control);
       (new TextBox() :> Control) |]

let control =
    let temp = moreControls.[0]
    temp.Text <- "Click Me!"
    temp

let button =
    let temp = (control :?> Button)
    temp.DoubleClick.Add(fun e -> MessageBox.Show("Hello") |> ignore)
    temp

```

此示例创建一个包含两个 Windows 控件对象的数组，将它们向上转换为其基类`Control`。接下来，它将第一个控件绑定到`control`标识符;将其转换为特定类型，`Button`;并为其`DoubleClick`事件添加处理程序 - `Control`类上不可用的事件。

## 摘要

您现在已经了解了如何使用 F# 中三种主要编程范例中的两种以及 F# 在任何样式混合中的编码方式。