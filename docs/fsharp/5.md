# 第 4 章类型和类型推断

F# 是强类型语言，这意味着您不能使用具有不合适值的函数。您不能使用整数参数调用具有字符串作为参数的函数;你必须明确地在两者之间转换。语言处理其值类型的方式称为类型系统x。 F# 有一个类型系统，不会妨碍常规编程。在 F# 中，所有值都有一个类型，这包括作为函数的值。

## 类型推断

通常，您不需要显式声明类型;编译器将根据函数中文字的类型以及它调用的其他函数的结果类型计算出值的类型。如果一切正常，编译器会将类型保留给自己;只有当类型不匹配时，编译器才会通过报告编译错误来通知您。该过程通常称为类型推断。如果您想了解有关程序中类型的更多信息，可以使编译器使用`–i`开关显示所有推断类型。 Visual Studio 用户获取工具提示，将鼠标指针悬停在标识符上时显示类型。

类型推理在 F# 中的工作方式相当容易理解。编译器通过程序工作，在定义标识符时将类型分配给标识符，从最左边的标识符开始向下到最右边。它根据已知的类型（即文字类型和（更常见的）其他源文件或程序集中定义的函数类型）分配类型。

下一个示例定义了两个 F# 标识符，然后使用 F# 编译器的`–i`开关显示控制台上显示的推断类型。

```
let aString = "Spring time in Paris"
let anInt = 42

```

val aString：string

val anInt：int

这两个标识符的类型分别不足为奇[ - COD0]和`int`。编译器用来描述它们的语法非常简单：关键字`val`（意为“值”），然后是标识符，冒号，最后是类型。

下一个例子中函数`makeMessage`的定义更有趣。

```
let makeMessage x = (Printf.sprintf "%i" x) + " days to spring time"
let half x = x / 2

```

```
val makeMessage: int -> string

val half: int -> int
```

请注意，`makeMessage`函数的定义以关键字`val`为前缀，就像您之前看到的两个值一样;即使它是一个函数，F# 编译器仍然认为它是一个值。此外，类型本身使用符号`int -> string`，这意味着函数采用整数并返回一个字符串。类型名称之间的`->`（ASCII 箭头）表示正在应用的函数的转换。箭头表示值的变换，但不一定是类型，因为它可以表示将值转换为相同类型的值的函数，如第二行的`half`函数所示。

可以部分应用的函数类型和带元组的函数不同。以下功能`div1`和`div2`说明了这一点。

```
let div1 x y = x / y
let div2 (x, y) = x / y

let divRemainder x y = x / y, x % y

```

```
val div1: int -> int -> int

val div2: int * int -> int

val divRemainder: int -> int -> int * int
```

函数`div1`可以部分应用，其类型为`int -> int -> int`，表示参数可以单独传递。将它与函数`div2`进行比较，它具有`int * int -> int`类型，意味着一个函数采用一对整数 - 一个整数元组 - 并将它们转换为一个整数。您可以在函数`div_remainder`中看到这一点，该函数执行整数除法并同时返回余数。它的类型是`int -> int -> int * int`，意味着一个返回整数元组的 curried 函数。

下一个函数`doNothing`看起来不够明显，但从打字的角度来看它非常有趣。

```
let doNothing x = x

```

val doNothing：'a - &gt; '一个

此函数具有`'a -&gt; 'a`类型，这意味着它采用一种类型的值并返回相同类型的值。任何以单引号（`'`）开头的类型都表示变量类型。 F# 有一个类型`obj`，映射到`System.Object`并表示任何类型的值 - 您可能会从其他基于公共语言运行时（CLR）的编程语言（实际上，许多语言）中熟悉这个概念不针对 CLR 的）。但是，变量类型不一样。注意该类型在箭头的两侧都有`'a`。这意味着，即使编译器还不知道类型，它也知道返回值的类型将与参数的类型相同。类型系统的这个特性，有时也称为类型参数化，允许编译器在编译时找到更多的类型错误，并且可以帮助避免转换。

| ![](img/note.png) | 注意：变量类型或类型参数化的概念与 CLR 2.0 版中引入的泛型的概念密切相关，现在已成为 CLI 版本 2.0 的 ECMA 规范的一部分。当 F# 以启用了泛型的 CLI 为目标时，它会在找到未确定类型的任何位置使用它们，从而充分利用它们。 F# 的创建者 Don Syme 在开始研究 F# 之前，在.NET CLR 中设计并实现了泛型。人们可能很想推断他是这样做的，所以他可以创造 F# ！ |

下一个示例中显示的函数`doNothingToAnInt`是受约束的值的示例 - 类型约束。在这种情况下，函数参数`x`被约束为`int`。可以将任何标识符（而不仅仅是函数参数）约束为某种类型，尽管更典型的是需要约束参数。此处的列表`stringList`显示了如何约束不是函数参数的标识符。

```
let doNothingToAnInt (x: int) = x
let intList = [1; 2; 3]

let (stringList: list<string>) = ["one"; "two"; "three"]

```

```
val doNothingToAnInt int: int -> int 

val intList: int list

val stringList: string list
```

将值约束为某种类型的语法很简单。在括号内，标识符名称后跟冒号（`:`），后跟类型名称。这有时也称为类型注释。

`intList`值是整数列表，标识符的类型是`int list`。这表明编译器已经识别出列表只包含整数，在这种情况下，其项目的类型不是未确定的，而是`int`。任何将`int`类型的值添加到列表之外的任何尝试都将导致编译错误。

标识符`stringList`具有类型注释。虽然这是不必要的，因为编译器可以从值中解析类型，但它用于显示使用未确定类型的替代语法。您可以在类型与之关联的类型之后将类型放在尖括号之间，而不是仅在类型名称之前写入它。请注意，即使`stringList`的类型被约束为`list<string>`（字符串列表），编译器在显示类型时仍会将其类型报告为`string list`，它们的含义完全相同。支持此语法，以使具有类型参数的 F# 类型看起来像来自其他.NET 库的泛型类型。

在编写纯 F# 时通常不需要约束值，尽管它偶尔会有用。当使用以 F# 以外的语言编写的.NET 库以及与非托管库的互操作时，它最有用。在这两种情况下，编译器都具有较少的类型信息，因此通常需要为其提供足够的信息来消除值的歧义。

## 定义类型

F# 中的类型系统提供了许多用于定义自定义类型的功能。所有 F# 的类型定义分为两类：

*   元组或记录，它们是一组用于形成复合类型的类型（类似于 C 中的结构或 C# 中的类）。
*   Sum 类型，有时也称为 union 类型。

## 元组和记录类型

元组是一种快速方便地将值组合成一组值的方法。值由逗号分隔，然后可以通过一个标识符引用，如下一个示例的第一行所示。然后，您可以通过反向检索值，如第二行和第三行所示，其中用逗号分隔的标识符出现在等号的左侧，每个标识符从元组接收单个值。如果要忽略元组中的值，可以使用`_`告诉编译器您对该值不感兴趣，如第二行和第三行中所示。

```
let pair = true, false
let b1, _ = pair
let _, b2 = pair

```

元组与 F# 中的大多数用户定义类型不同，因为您不需要使用`type`关键字显式声明它们。要定义类型，请使用`type`关键字，后跟类型名称，等号，然后使用您定义的类型。在最简单的形式中，您可以使用它来为任何现有类型（包括元组）提供别名。为单个类型提供别名通常并不常用，但为元组提供别名可能非常有用，尤其是当您想要使用元组作为类型约束时。下一个示例显示如何为单个类型和元组提供别名，以及如何将别名用作类型约束。

```
type Name = string
type Fullname = string * string

let fullNameToSting (x: Fullname) =
    let first, second = x in
    first + " " + second

```

记录类型类似于元组，因为它们将多种类型组合成单一类型。不同之处在于，在记录类型中，每个字段都被命名。下一个示例说明了定义记录类型的语法。

```
// Define an organization with unique fields.
type Organization1 = { boss: string; lackeys: string list }
// Create an instance of this organization.
let rainbow =
    { boss = "Jeffrey";
      lackeys = ["Zippy"; "George"; "Bungle"] }

// Define two organizations with overlapping fields.
type Organization2 = { chief: string; underlings: string list }
type Organization3 = { chief: string; indians: string list }

// Create an instance of Organization2.
let (thePlayers: Organization2) =
    { chief = "Peter Quince";
      underlings = ["Francis Flute"; "Robin Starveling";
                    "Tom Snout"; "Snug"; "Nick Bottom"] }

// Create an instance of Organization3.
let (wayneManor: Organization3) =
    { chief = "Batman";
      indians = ["Robin"; "Alfred"] }

```

您将字段定义放在大括号之间，并用分号分隔。字段定义由字段名称后跟冒号和字段类型组成。类型定义`Organization1`是一种记录类型，其中字段名称是唯一的。这意味着您可以使用简单的语法来创建此类型的实例，其中创建时无需提及类型名称。要创建记录，请将字段名称后跟等号和大括号之间的字段值（`{}`），如`Rainbow`标识符所示。

F# 不强制字段名称是唯一的，因此有时编译器不能仅从字段名称推断字段的类型。在这种情况下，编译器无法推断记录的类型。要使用非唯一字段创建记录，编译器需要静态地知道正在创建的记录的类型。如果编译器无法推断记录的类型，则需要使用[类型推断](#_Type_Inference)部分中描述的类型注释。使用类型注释由`Organization2`和`Organization3`类型及其实例`thePlayers`和`wayneManor`说明。您可以在其名称后面看到明确指定的标识符类型。

访问记录中的字段非常简单。您只需使用语法`record`标识符名称，后跟一个点，后跟字段名称。以下示例说明了这一点，显示了如何访问`Organization`记录的`chief`字段。

```
// Define an organization type.
type Organization = { chief: string; indians: string list }

// Create an instance of this type.
let wayneManor =
    { chief = "Batman";
      indians = ["Robin"; "Alfred"] }

// Access a field from this type.
printfn "wayneManor.chief = %s" wayneManor.chief

```

默认情况下，记录是不可变的。对于命令式程序员来说，这可能听起来像记录不是很有用，因为不可避免地会出现需要更改字段值的情况。为此，F# 提供了一种简单的语法，用于创建具有更新字段的记录副本。要创建记录的副本，请将该记录的名称放在大括号后跟关键字`with`，然后是要更改的字段列表及其更新值。这样做的好处是您无需重新键入未更改的字段列表。以下示例演示了此方法。它创建`wayneManor`的初始版本，然后创建`wayneManor'`，其中`"Robin"`已被删除。

```
// Define an organization type.
type Organization = { chief: string; indians: string list }

// Create an instance of this type.
let wayneManor =
    { chief = "Batman";
      indians = ["Robin"; "Alfred"] }

// Create a modified instance of this type.
let wayneManor' =
    { wayneManor with indians = [ "Alfred" ] }

// Print out the two organizations.
printfn "wayneManor = %A" wayneManor
printfn "wayneManor' = %A" wayneManor'

```

编译和执行时，此示例的结果如下：

wayneManor = {chief =“Batman”;

印地安人= [“罗宾”; “阿尔弗雷德”];}

wayneManor'= {chief =“Batman”;

印地安人= [“阿尔弗雷德”];}

访问记录中字段的另一种方法是使用模式匹配;也就是说，您可以使用模式匹配来匹配记录类型中的字段。正如您所料，使用模式匹配检查记录的语法类似于用于构造它的语法。您可以使用字段`=`常量将字段与常量进行比较。您可以使用字段`=`标识符分配带有标识符的字段值。您可以忽略字段`=`的字段。以下示例中的`findDavid`函数说明了使用模式匹配来访问记录中的字段。

```
// Type representing a couple.
type Couple = { him : string ; her : string }

// List of couples.
let couples =
    [ { him = "Brad" ; her = "Angelina" };
      { him = "Becks" ; her = "Posh" };
      { him = "Chris" ; her = "Gwyneth" };
      { him = "Michael" ; her = "Catherine" } ]

// Function to find "David" from a list of couples.
let rec findDavid l =
    match l with
    | { him = x ; her = "Posh" } :: tail -> x
    | _ :: tail -> findDavid tail
    | [] -> failwith "Couldn't find David"

// Print the results.
printfn "%A" (findDavid couples)

```

`findDavid`功能中的第一条规则是执行实际工作的规则，检查记录的`her`字段以查看它是否是`"Posh"`，David 的妻子。 `him`字段与标识符`x`相关联，因此可以在规则的后半部分使用。

编译和执行时，此示例的结果如下：

小贝

重要的是要注意，当您对这样的记录进行模式匹配时，您只能使用文字值。因此，如果您想要通用该功能以允许您更改要搜索的人，则需要在模式匹配中使用`when`保护：

```
let rec findPartner soughtHer l =
    match l with
    | { him = x ; her = her } :: tail when her = soughtHer -> x
    | _ :: tail -> findPartner soughtHer tail
    | [] -> failwith "Couldn't find him"

// Print the results.
printfn "%A" (findPartner "Angelina" couples )

```

字段值也可以是函数，这有时可以用于提供类似对象的行为，因为记录的每个记录实例可以具有不同的函数实现。

## 联盟或总和类型

联合类型，有时称为和类型或区分联合，是将可能具有不同含义或结构的数据汇集在一起​​的一种方式。

您可以使用`type`关键字定义联合类型，后跟类型名称，后跟等号 - 与所有类型定义相同。接下来是通过管道分离的不同构建体的定义。第一个管道是可选的。

构造函数由一个必须以大写字母开头的名称组成，旨在避免将构造函数名称与标识符名称混淆的常见错误。该名称可以选择后跟关键字`of`，然后是构成该构造函数的类型。构成构造函数的多个类型由星号分隔。类型中构造函数的名称必须是唯一的。如果定义了几个联合类型，那么它们的构造函数的名称可以重叠;但是，在执行此操作时应该小心，因为在构造和使用联合类型时可能需要进一步的类型注释。

下一个示例定义了一个类型`Volume`，其值可以有三种不同的含义：升，美国品脱或英制品脱。虽然数据的结构是相同的并且由浮点数表示，但其含义却截然不同。在算法中混淆数据的含义是程序中错误的常见原因，`Volume`类型在某种程度上是试图避免这种情况。

```
type Volume =
    | Liter of float
    | UsPint of float
    | ImperialPint of float

let vol1 = Liter 2.5
let vol2 = UsPint 2.5
let vol3 = ImperialPint (2.5)

```

构造联合类型的新实例的语法是构造函数名称，后跟类型的值，其中多个值以逗号分隔。 （可选）您可以将值放在括号中。您使用三个不同的`Volume`构造函数来构造三个不同的标识符：`vol1`，`vol2`和`vol3`。

要将联合类型的值解构为其基本部分，您始终使用模式匹配。当联合类型上的模式匹配时，构造函数构成模式匹配规则的前半部分。您不需要完整的规则列表，但如果列表不完整，则必须使用标识符或通配符来匹配所有剩余规则的默认规则。构造函数规则的第一部分由构造函数名称后跟标识符或通配符组成，以匹配其中的各种值。以下`convertVolumeToLiter`，`convertVolumeUsPint`和`convertVolumeImperialPint`函数演示了以下语法：

```
// Type representing volumes.
type Volume =
    | Liter of float
    | UsPint of float
    | ImperialPint of float

// Various kinds of volumes.
let vol1 = Liter 2.5
let vol2 = UsPint 2.5
let vol3 = ImperialPint 2.5

// Some functions to convert between volumes.
let convertVolumeToLiter x =
    match x with
    | Liter x -> x
    | UsPint x -> x * 0.473
    | ImperialPint x -> x * 0.568
let convertVolumeUsPint x =
    match x with
    | Liter x -> x * 2.113
    | UsPint x -> x
    | ImperialPint x -> x * 1.201
let convertVolumeImperialPint x =
    match x with
    | Liter x -> x * 1.760
    | UsPint x -> x * 0.833
    | ImperialPint x -> x

// A function to print a volume.
let printVolumes x =
    printfn "Volume in liters = %f,
in us pints = %f,
in imperial pints = %f"
        (convertVolumeToLiter x)
        (convertVolumeUsPint x)
        (convertVolumeImperialPint x)
// Print the results.
printVolumes vol1
printVolumes vol2
printVolumes vol3

```

此问题的另一种解决方案是使用 F# 的度量单位，它允许将类型应用于数值。

## 类型定义与类型参数

联合和记录类型都可以参数化。参数化类型意味着将所定义的类型中的一个或多个类型留给稍后由类型的消费者确定。这与本章前面讨论的变量类型的概念类似。在定义类型时，您必须更明确地了解哪些类型是可变的。

要创建一个或多个参数类型，请将类型名称后面的参数化类型放在尖括号中，如下所示：

```
type BinaryTree<'a> =
| BinaryNode of 'a BinaryTree * 'a BinaryTree
| BinaryValue of 'a

let tree1 =
    BinaryNode(
        BinaryNode ( BinaryValue 1, BinaryValue 2),
        BinaryNode ( BinaryValue 3, BinaryValue 4) )

```

与变量类型一样，类型参数的名称始终以单引号（`'`）开头，后跟类型的字母数字名称。通常，只使用一个字母。如果需要多个参数化类型，请用逗号分隔它们。然后，您可以在整个类型定义中使用类型参数。

创建和使用参数化类型实例的语法与创建和使用非参数化类型的语法不同。这是因为编译器会自动推断参数化类型的类型参数。您可以在`tree1`的以下构造中看到这一点，并通过函数`printBinaryTreeValues`消耗它们：

```
// Definition of a binary tree.
type BinaryTree<'a> =
    | BinaryNode of 'a BinaryTree * 'a BinaryTree
    | BinaryValue of 'a

// Create an instance of a binary tree.
let tree1 =
    BinaryNode(
        BinaryNode ( BinaryValue 1, BinaryValue 2),
        BinaryNode ( BinaryValue 3, BinaryValue 4) )

// Function to print the binary tree.
let rec printBinaryTreeValues x =
    match x with
    | BinaryNode (node1, node2) ->
        printBinaryTreeValues node1
        printBinaryTreeValues node2
    | BinaryValue x ->
        printf "%A, " x

// Print the results.
printBinaryTreeValues tree1

```

编译和执行时，此示例的结果如下：

1,2,3,4，

您可能已经注意到，虽然我已经讨论过定义类型，创建它们的实例以及检查这些实例，但我还没有讨论过更新它们。不可能更新这些类型，因为随时间变化的值的想法违背了函数式编程的想法。

## 摘要

这是对如何在 F# 中创建类型的简要介绍。您会发现 F# 的类型系统提供了一种灵活的方式来表示程序中的数据。